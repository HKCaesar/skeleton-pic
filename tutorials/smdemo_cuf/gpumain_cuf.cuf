! GPU vector add test program for CUDA Fortran
! written by Viktor K. Decyk, UCLA/
      module gpumain_cuf
      use cudafor
      implicit none
!
      integer :: nblock_size = 64, ngrid_size = 1
      integer :: devid
      save
!
      contains
!
      attributes(global) subroutine emptyKernel()
      end subroutine
!
!-----------------------------------------------------------------------
      subroutine setgbsize(nblock)
      integer :: nblock
      nblock_size = nblock
      end subroutine
!
!-----------------------------------------------------------------------
      subroutine g_fallocate(g_f,nsize,irc)
! allocate global real memory on GPU
      implicit none
      real, device, dimension(:), allocatable :: g_f
      integer :: nsize, irc
      integer :: crc
      crc = cudaMalloc(g_f,nsize)
      if (crc /= 0) then
         write (*,*) 'cudaMalloc real Error=', crc, ':',                &
     &cudaGetErrorString(crc), ',l=', nsize
         irc = 1
      endif
      end subroutine
!
!-----------------------------------------------------------------------
      subroutine g_deallocate(g_f,irc)
! deallocate global memory on GPU
      implicit none
      real, device, dimension(:) :: g_f
      integer :: irc
      integer :: crc
      crc = cudaFree(g_f)
      if (crc /= 0) then
         write (*,*) 'cudaFree real Error=', crc, ':',                  &
     &cudaGetErrorString(crc)
         irc = 1
      endif
      end subroutine
!
!-----------------------------------------------------------------------
      subroutine copyin_gmemptr(f,g_f,nsize)
! copy real array from main memory to global GPU memory
      implicit none
      real, dimension(:) :: f
      real, device, dimension(:) :: g_f
      integer :: nsize
      integer :: crc
      crc = cudaMemcpy(g_f,f,nsize,cudaMemcpyHostToDevice)
      if (crc /= 0) then
         write (*,*) 'cudaMemcpyHostToDevice real Error=', crc, ':',    &
     &cudaGetErrorString(crc)
         stop
      endif
      end subroutine
!
!-----------------------------------------------------------------------
      subroutine copyout_gmemptr(f,g_f,nsize)
! copy real array from global GPU memory to main memory
      implicit none
      real, dimension(:) :: f
      real, device, dimension(:) :: g_f
      integer :: nsize
      integer :: crc
      crc = cudaMemcpy(f,g_f,nsize,cudaMemcpyDeviceToHost)
      if (crc /= 0) then
         write (*,*) 'cudaMemcpyDeviceToHost real Error=', crc, ':',    &
     &cudaGetErrorString(crc)
         stop
      endif
      end subroutine
!
!-----------------------------------------------------------------------
      subroutine empty_kernel()
      implicit none
      integer :: crc, ngx, ngy
      type (dim3) :: dimBlock, dimGrid
      ngx = min(nblock_size,32768)
      ngy = (ngrid_size - 1)/ngx + 1
      dimBlock = dim3(nblock_size,1,1)
      dimGrid = dim3(ngx,ngy,1)
      crc = cudaGetLastError()
      call emptyKernel<<<dimGrid,dimBlock>>>()
      crc = cudaThreadSynchronize()
      crc = cudaGetLastError()
      if (crc /= 0) then
         write (*,*) 'emptyKernel real Error=', crc, ':',               &
     &cudaGetErrorString(crc)
         stop
      endif
      end subroutine
!
!-----------------------------------------------------------------------
      attributes(global) subroutine gadd(a,b,c,nx)
      implicit none
      integer, value :: nx
      real :: a(nx), b(nx), c(nx)
      integer :: j
      j = threadIdx%x+blockDim%x*(blockIdx%x-1)
      if (j <= nx) a(j) = b(j) + c(j)
      end subroutine
!
!-----------------------------------------------------------------------
      subroutine init_cu(dev,irc)
! initialize CUDA for best GPU available
! searches throughs devices, selects the device with the most compute
! units, and saves the device id devid
! error code is modified only if there is an error
      implicit none
      integer :: dev, irc
! local data
      integer :: maxcpus = 0, jm = -1
      integer :: crc, j, js, ndevs, maxunits
      integer(kind=8) :: msize
      double precision :: z
      type (cudaDeviceProp) :: prop
! returns number of device
      crc = cudaGetDeviceCount(ndevs)
      if (crc /= 0) then
         write (*,*) 'cudaGetDeviceCount Error=', crc, ':',             &
     &cudaGetErrorString(crc)
         irc = 1
      endif
! get information about devices
      do j = 1, ndevs
      js = j - 1
      crc = cudaGetDeviceProperties(prop,js)
      if (crc /= 0) then
         write (*,*) 'cudaGetDeviceProperties Error=', crc, ':',        &
     &cudaGetErrorString(crc)
         prop%name = ''
      endif
      maxunits = prop%multiProcessorCount
      write (*,*) 'j=', js, ':CUDA_DEVICE_NAME=', trim(prop%name)
      write (*,*) '    CUDA_MULTIPROCESSOR_COUNT=', maxunits
      msize = prop%totalGlobalMem
      z = dble(msize)/1073741824.0d0
      write (*,*) '    CUDA_GLOBAL_MEM_SIZE=',msize,'(',real(z),'GB)'
      if (maxunits > maxcpus) then
         maxcpus = maxunits
         jm = js
      endif
      enddo
      devid = jm
      if ((dev >= 0) .and. (dev < ndevs)) devid = dev
      write (*,*) 'using device j=', devid
! set device
      crc = cudaSetDevice(devid)
      if (crc /= 0) then
         write (*,*) 'cudaSetDevice Error=', crc, ':',                  &
     &cudaGetErrorString(crc)
         irc = 1
      endif
! run empty kernel
      call empty_kernel()
      end subroutine
!
!-----------------------------------------------------------------------
      subroutine gpadd(a,b,c,nx)
! Vector Add Interface
      implicit none
      integer :: nx
      real, device, dimension(:) :: a(nx), b(nx), c(nx)
      integer :: crc
      type (dim3) :: dimBlock, dimGrid
      dimBlock = dim3(nblock_size,1,1)
      dimGrid = dim3((nx-1)/nblock_size+1,1,1)
      call gadd<<<dimGrid,dimBlock>>>(a,b,c,nx)
      crc = cudaThreadSynchronize()
      end subroutine
!
!-----------------------------------------------------------------------
      subroutine end_cu()
      implicit none
      integer :: crc
      crc = cudaThreadExit()
      if (crc /= 0) then
         write (*,*) 'cudaThreadExit Error=', crc, ':',                 &
     &cudaGetErrorString(crc)
      endif
      end subroutine
!
      end module
